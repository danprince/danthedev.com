---
title: Why Zig?
---

Last year I learned Zig for [Advent of Code](https://adventofcode.com/) as part of an ongoing tradition to solve the puzzles in a different programming language each year. Sometimes I pick a language I'm relatively comfortable with (Clojure, Go) because I don't get a chance to work with it as often as I would like, and other years, I pick a language that I have little/no experience with (Zig, Nim, Julia, Rust), to see what it's all about.

I don't think Zig is actually a particularly good fit for solving these kinds of puzzles, but what really impressed me is how expressive and elegant it can be, considering its simple semantics and low level programming model.

I actually enjoyed writing Zig so much for the daily puzzles that I started a few Zig side projects, as an excuse to dive deeper into some aspects of the language that are less important when writing puzzle solutions.

Here's what I think Zig gets right, in no particular order.

## Testing
Modern programming languages should all ship with a built in testing framework. This is a critical components in a cohesive language ecosystem and Zig delivers.

Running `zig test` will run all tests that you specify in your [build.zig](https://ziglang.org/learn/overview/#zig-build-system) or you can run `zig test file.zig` to test a specific file.

When running tests, Zig runs these `test` blocks in your source files.

```zig
test "examples" {
    try testing.expectEqual(2, 1 + 1);
}
```

The [testing namespace](https://ziglang.org/documentation/master/std/#root;testing) in the standard library contains everything you need to write assertions when comparing all sorts of values, pointers, optionals, errors.

It also contains a custom allocator that will report memory leaks. This is actually kind of a big deal, as Zig's manual memory management means every single allocation is explicit. So long as you are writing tests, `testing.allocator` makes sure that you don't forget to free memory, which eliminates a significant class of bugs.

These tests can also (and should regularly) be used inline within source files, because they become an invaluable source of compiler-checked documentation and examples.

Unfortunately `zig test` does not support a watch mode, so I usually configure `zig test` as [my default test task in VSCode](https://github.com/danprince/zigrl/blob/e75c5d02b99ea78bfb3abbe1069a305b9c5c7cee/.vscode/tasks.json#L29-L46), which I bind to a keyboard shortcut, making it easy to run my tests from anywhere in the project.

The only real problem I found was that the argument ordering of the assertion functions. Here's the definition of `expectEqual` straight out of the v0.11 source:

```zig
pub fn expectEqual(expected: anytype, actual: @TypeOf(expected)) !void {
```

Unlike most languages, the `expected` value comes second, which can make writing tests feel like ["Yoda conditions"](https://en.wikipedia.org/wiki/Yoda_conditions). There are [reasons for this](https://github.com/ziglang/zig/issues/4437) but I often forgot to swap the arguments and just got used to reading the output backwards.

## Control Flow
In many programming languages there's an _apparent_ control flow. Code moves from statement to statement, going through loops, and only jumping away when functions are called. In most programming languages there's also a _hidden_ control flow. Some languages have exceptions which might halt execution when something further up the stack frame throws and others implement syntatic overloads which may call out to user defined routines. The worst offenders are usually languages that support macros, because there's really no telling what might happen when you call those.

That _hidden_ control flow stuff? Zig doesn't have any of it. Function calls are the only time that you have to think about the consequences of ending up in a new stack frame. This allows you to rule out a whole class of possibilities when you are debugging. It forces you to design more explicit interfaces and to rely less on language level magic that might not be obvious to less experienced programmers.

This holistic approach to simplicity shows up again and again when comparing the language to some of its competitors. I was able to become productive quite quickly in Zig, and to retain that productivity, even after I stopped writing code every day.

## Errors & Missing Values
Rather than making errors a separate control flow construct, Zig makes them into first class values that can be returned by any function. There are a few languages that use a similar model but there's an important distinction; Zig errors are a separate _kind_ of value.

Let's look at Go, another language which uses errors-as-values, to understand the difference.

```go
func random() (float64, error) {
    // ...
}
```

This `random` function returns two values and the second one happens to be an error. There's nothing to stop you from swapping the return signature, and error-last is just a convention. The compiler doesn't recognise a semantic difference from returning two floats, so it can't help enforce the pattern.

Then it's up to the caller to recognise the error value and to handle it accordingly.

```go
val, err := random();

if err != nil {
  // ...
}
```

This pattern is brilliantly simple, but it adds an additional 3+ lines of code every single time you call a function that may return an error, and this quickly contributes to a general level of noise in functions.

Here's how the same signature would look in Zig.

```zig
fn random() !f64 {
    // ...
}
```

The `!` in the signature tells us that the function can return error value _or_ a floating point value. There's no convention here, if your function can error, that error value must be encoded into the return type _as an error_.

Callers don't have to worry about unpacking multiple values and checking them individually, instead they receive a single value that can be an error _or_ a value. This might not seem particularly impressive if you've used languages with sum/union types but it really shines with control flow primitives that are designed to work with error unions.

`if` statements have capturing behaviour that can unwrap error unions.

```zig
if (random()) |val| {
  // val is an f64
} else |err| {
  // err is an error
}
```

`catch` statements give you a chance to provide an default value without a full control flow branch.

```zig
const number = random() catch 42;
```

And `try` (the mostly commonly used) is a shortcut for returning the error from inside a catch branch.

```zig
try random();
// is equivalent to writing
// random() catch |err| return err;
```

Propagating errors in Zig adds very little noise, especially considering that it is manual and explicit; there are no exceptions or recoverable panics.

The same kind of approach is used for handling optional values. Functions must signify that their return is an optional type with a `?` if they want to return a potentially `null` value.

```zig
fn maybe() ?bool {
  // ...
}
```

Because the compiler knows that a `?f64` is not a `f64` it will prevent you from accidentally using it and again you can make use of syntax primitives for handling the optional appropriately.

`if` also understands how to capture optional values.

```zig
if (maybe()) |val| {
  // val is bool
} else {
  // val was null
}
```

So does `while`, enabling a simple pattern for writing iterators.

```zig
// When `tokens.next()` returns `null`, the loop stops.
while (tokens.next()) |token| {
  // ...
}
```

There's also `orelse` which gives you a chance to provide a default value.

```zig
const answer = maybe() orelse false;
```

There's also a short-form of `orelse` which will assume the value is not null, and panic if it is.

```zig
const answer = maybe().?;
```

When error handling is too explicit in a programming language, it becomes tedious and people look for ways to avoid or abstract it. On the other hand, when error handling is too implicit then people forget to do it.

Zig strikes an incredible blend between explicit errors and pragmatic simplicity, with the  best model for error handling and optional values that I've ever used in a programming language.

## Standard Library
To talk about the standard library properly, we have to talk about generics. Fortunately, Zig's generics are one of the most interesting things about the language.

In other languages you might be used to parameterising types and function calls with an angle bracket syntax. For example `ArrayList<number>` might define an array of numbers. In Zig, this would be `ArrayList(f64)`.

This isn't a special syntax exception. Types can be passed around like any other parameters.

You see this pattern throughout the standard library. For example, the `std.mem` namespace is largely made up of functions that operate on arrays, but because Zig accepts types as parameters, there is no need for function overloading.

```zig
std.mem.split(u8, input, "\n");
```

The `split` function above returns a `SplitIterator(u8)` which will loop over the lines in the `input` string.

Generics are nothing new, but they can still be a controversial aspect of language design, with plenty of high profile examples of languages that either don't implement them, or overcomplicate them. For me, Zig finds a near perfect balance in the middle.

Zig offers a solid set of data structures including hash maps, array lists, sets/bitsets, priority queues, tail queues, and bloom filters. For a low level language, this is impressive, and goes a long way towards the front page claim that "Zig is a general-purpose programming language".

For application level programming, Zig still falls a long way short of a language like Go, which provides much higher level interfaces for working with HTTP, SQL, regular expressions, templating languages, image processing, and more out of the box.

Currently, the documentation for the standard library isn't good. The official documentation even has a big red banner which acknowledges this and suggests that you read the standard library's source code instead. This will be a dealbreaker for some people, and fair enough, but if you decide to persevere then you'll find an excellent codebase, clearly written, well commented, and full of inline tests that serve as examples.

Any editor with support for the language server protocol should make it a one step process to jump to the definition of any type or function from the standard library, and quickly hopping backwards and forwards between my own code and the code I'm using from the standard library has been an invaluable part of learning the language.

## Containers
Many syntatic blocks (like structs) can hold their own variable and function declarations. This seemed a little arbitrary to me at first. Here's a struct that has two properties:

```zig
const Vec = struct {
    x: f64,
    y: f64,
};
```

No surprises here. Things get interesting when we introduce a declaration alongside those properties.

```zig
const Vec = struct {
    x: f64,
    y: f64,

    fn of(x: f64, y: f64) Vec {
        return .{ .x = x, .y = y };
    }

    fn size(v: Vec) Vec {
      return std.math.hypot(f64, v.x, v.y);
    }
};
```

We can refer to these functions as static member of `Vec`.

```zig
const v = Vec.of(1, 2);
```

And we can refer to them as members on instances of `Vec` (so long as the types line up)!

```zig
const size = Vec.of(1, 2).size();
```

This is probably the most sensible implementation of static members and methods that I remember seeing. It not limited to structs either. Enums, unions, and opaques are also [containers](https://ziglang.org/documentation/master/#Containers).

Here's a real example from Advent of Code.

```zig
const Sign = enum {
    rock,
    paper,
    scissors,

    fn parse(ch: u8) Sign {
        return switch (ch) {
            'A', 'X' => .rock,
            'B', 'Y' => .paper,
            'C', 'Z' => .scissors,
            else => unreachable,
        };
    }

    fn score(sign: Sign) u32 {
        return switch (sign) {
            .rock => 1,
            .paper => 2,
            .scissors => 3,
        };
    }
}
```

If Zig didn't support containers, then these complementary functions would probably need some manual namespacing (for example `parseSign` and `scoreSign`) to prevent collisions with functions from other types in the same scope.

## Everything Else
This wasn't supposed to be a deep dive on Zig, so I've pulled together quick overviews of other things that I appreciate about the language.

- It comes with a built in formatter (`zig fmt`).
  - The formatter respects a trailing comma to decide whether to break lines.
- Testing and compiling is fast.
- Declaration privacy is module level (other code in the same file can read private properties of adjacent structs).
- Strong semantic consistency across declarations.
  - Everything is `const x = ...` (imports, structs, enums, type aliases, unions, error).
- Structs [can work as namespaces](https://github.com/danprince/zigrl/blob/e75c5d02b99ea78bfb3abbe1069a305b9c5c7cee/src/handler.zig#L19-L55).
- [Files are structs](https://zig.news/gowind/zig-files-are-structs-288j)
- Most syntactic constructs are expressions (think `const x = if (true) 42 else 24`).
- Compiling Zig from source is [straightforward](https://github.com/ziglang/zig/wiki/Building-Zig-From-Source).
- The [language reference](https://ziglang.org/documentation/master/) is high-quality and interesting.
- Creating a compile time distinction between [pointers and many-pointers](https://ziglang.org/documentation/0.10.0/#Pointers) removes a whole class of problems.
- There is no [variable shadowing](https://ziglang.org/documentation/0.10.0/#Shadowing). This removes another significant class of subtly confusing problems.
- It's relatively easy to compile to WebAssembly and interface with JavaScript code in a browser ([example project](https://github.com/danprince/zig-wasm-life)).
- [Ziglings](https://github.com/ratfactor/ziglings) is a great set of exercises for learning the language quickly.
- The community is approachable, friendly, and active. The Zig Discord server is a good place to ask questions and see what's going on across the language.

## Rough Edges
Zig still isn't a mature or particularly stable programming language (seems like almost everyone works off of Zig's `master` branch), even if it is heading in the right direction, and I don't want to pile too much praise on it without recognising some of the pain points that I struggled with too.

- There's no concept of interfaces or constraining generic types and the workarounds ([1](https://ethanfrei.com/posts/zig-interfaces.html), [2](https://zig.news/kilianvounckx/zig-interfaces-for-the-uninitiated-an-update-4gf1), [3](https://www.nmichaels.org/zig/interfaces.html)) aren't intuitive.
- A general lack of blog posts and Stack Overflow questions means that googling isn't often helpful. You have to get comfortable reading the source.
- Generated documentation isn't good. You have to get comfortable reading the source.
- Whilst usually good, some compile errors are still rough to interpret and action. I even managed to crash the compiler itself once or twice.
- Compile time programming errors don't have the kinds of stack traces you might expect, making it hard to track down when you make a mistake in something like a `std.debug.print` call.
- I wasn't able to get breakpoints and a step-debugger working inside my editor. Might just be unfamiliarity with LLDB on my part.
- The editor support is generally a bit patchy.
  - It took quite a lot of work to get ZLS working inside VSCode and even then, some errors aren't detected and highlighted until I manually compile.
  - Some code paths have working intellisense and others don't.
  - Code that depends on a compile time branch doesn't work with intellisense.

## When?
I don't write low level programs enough to remember all the footguns of a language like C, or all of the complexities of a language like Rust. Zig fits comfortably in the middle, with semantics that are easy to approach and easy to remember.

I think Zig is a great language for people who are interested in exploring lower level programming without a steep barrier to entry. This will be especially true as the language continues to smooth out the rough edges mentioned above.
