---
title: Day 3
---

I saw binary strings, got excited, and immediately fell into the trap of learning my way around [`bitops`][bitops] before I made a proper start on the first part of day 3.

Bitwise operations aren't commonly used in most programming languages, so it has always felt odd to me that single character operators are often reserved for them, such as `^`, `|`, and `&`. Nim makes (in my opinion) the sensible decision to use more verbose names like `bitnot`, `bitor`, and `bitand`. They also live in a separate module, rather than include them in `system`.

The rough gist of the first part of the problem was to find the most common bit in each position, across a list of binary strings.

```
00100
11110
10110
=
10110
```

Turning the strings into numbers didn't help at all here, and just convoluted the logic, requiring shifts/masks to check the values of each bit. With repeated runs over an enormous dataset, it would probably be faster, but for my puzzle input (1000 numbers) checking the characters directly was plenty fast enough.

I started out with a `mostCommonBit` function that would return the most common bit for a sequence of strings, given a position.

```nim
proc mostCommonBit(numbers: seq[string], index: int): char =
  var ones = 0
  for number in numbers:
    if number[index] == '1':
      ones += 1
  let zeroes = numbers.len - ones
  if ones >= zeroes: '1' else: '0'
```

Nothing particularly interesting here. Despite having borrowed a lot from Python's syntax, one place where Nim differs stylistically is with identifier names, opting for `camelCase` rather than `snake_case`, like Python. I tend to gravitate towards more explicit names unless I'll need to type them lots of times, and I prefer that when they're broken with punctuation. My favourite casing of all is Lisp's `kebab-case`.

The final solution called `mostCommonBit` for each position, then parsed the resulting string as a binary number. The other part of the problem required you to find the least common bit for each position, so I built two versions of an `invert` procedure that would flip a single bit, or an entire string of bits, to get the complement to `mostCommonBit`.

```nim
proc invert(c: char): char =
  if c == '0': '1' else: '0'

proc invert(str: string): string =
  str.map(invert).join
```

## Part 2
The second part put a slightly more interesting spin on the problem. Using the same logic for finding most/least common bits, we'd find the median value, by iterating through the positions and discarding any values that _didn't_ have a bit that matched the most/least common bit in that position.

I made a bit of a mistake thinking that I could solve the function once, then `invert` the result to solve both parts, but the inversion logic doesn't hold true for this new variant of the problem.

I ended up with two similar functions that bumped the `invert` operation one level lower, instead.

```nim
proc findMostCommon(numbers: seq[string]): string =
  let width = numbers[0].len
  var queue = numbers
  for i in 0 ..< width:
    let bit = mostCommonBit(queue, i)
    queue = queue.filterIt(it[i] == bit)
    if queue.len == 1:
      return queue[0]

proc findLeastCommon(numbers: seq[string]): string =
  let width = numbers[0].len
  var queue = numbers
  for i in 0 ..< width:
    let bit = mostCommonBit(queue, i).invert
    queue = queue.filterIt(it[i] == bit)
    if queue.len == 1:
      return queue[0]
```

I wasn't super happy with this duplication, but it felt like it would have been unnatural to support both behaviours with one parameterized procedure by comparison.

It probably won't be the last time I touch on it, but I'm really not sure about the `It` functions from `setutils`.

```nim/1
let bit = mostCommonBit(queue, i).invert
queue = queue.filterIt(it[i] == bit)
if queue.len == 1:
  return queue[0]
```

This `filterIt` macro is essentially just a shorthand way to write a more verbose proc passing version of `filter`.

```nim/1-2
let bit = mostCommonBit(queue, i).invert
queue = queue.filter(proc (it: string): bool =
  it[i] == bit)
if queue.len == 1:
  return queue[0]
```

The syntax for inline procs isn't great, but this has only really shown up in the `sequtils` module so far. It doesn't particularly feel like Nim wants you to use a functional proc-passing style. Even the docs for [`filterIt`](https://nim-lang.org/docs/sequtils.html#filterIt.t%2Cuntyped%2Cuntyped) suggest using the [`collect`](https://nim-lang.org/docs/sugar.html#collect.m%2Cuntyped%2Cuntyped) macro instead.

```nim
queue = queue.filterIt(it[i] == bit)
# vs
collect:
  for str in queue:
    if str[i] == bit: str
```

For more complex expressions I can see myself using `collect`, as you lose a lot of meaning when you go from a name to `it`, but the syntax is also a little bit clunky for short expressions.

Anyway, here's the code!

[![GitHub](/icons/github.svg) Day 3](https://github.com/danprince/advent-of-code/blob/master/2021/day-03/main.nim){.center}

[bitops]: https://nim-lang.org/docs/bitops.html
